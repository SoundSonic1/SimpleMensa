apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: "org.jlleitschuh.gradle.ktlint"

apply plugin: 'jacoco-android'

android {
    signingConfigs {
        release {
            storeFile file('simplemensakey.jks')
            keyAlias = 'RedFruitKey'
            storePassword '123456'
            keyPassword 'qwertzu'
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.soundsonic.simplemensa"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField 'String', "MAPBOX_TOKEN", MAPBOXTOKEN
        }
        release {
            buildConfigField 'String', "MAPBOX_TOKEN", MAPBOXTOKEN
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    def dagger_version = '2.25.4'
    def retrofit_version = '2.7.1'
    def moshi_version = '1.9.2'
    def lifecycle_version = '2.2.0'
    def junit_version = '5.5.2'
    def room_version = "2.2.3"


    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation "androidx.activity:activity-ktx:1.1.0"
    implementation "androidx.fragment:fragment-ktx:1.2.0"
    implementation "androidx.preference:preference-ktx:1.1.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation 'jp.wasabeef:recyclerview-animators:3.0.0'

    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation("com.squareup.moshi:moshi-kotlin:$moshi_version")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:$moshi_version")

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    implementation("io.coil-kt:coil:0.9.2")

    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:8.6.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-core:1.3.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v8:0.11.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-android-plugin-annotation-v8:0.7.0'

    testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

ktlint {
    android.set(true)

    reporters {
        customReporters {
            register("html") {
                fileExtension = "html"
                dependency = "me.cassiano:ktlint-html-reporter:0.2.3"
            }
        }
    }
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoAndroidUnitTestReport {
    excludes += ['**/*Fragment*.*',
                 '**/*Activity*.*',
                 '**/*App.*',
                 '**/*ViewHolder*.*',
                 '**/*VMFactory*.*',
                 '**/*ListAdapter*.*',
                 '**/*PagerAdapter*.*',
                 '**/*BindingAdapter*.*',
                 '**/*HandlerImpl*.*',
                 '**/*Constants*.*',
                 '**/*Module*.*']
}
